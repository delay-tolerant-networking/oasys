dnl
dnl    Copyright 2007 Intel Corporation
dnl 
dnl    Licensed under the Apache License, Version 2.0 (the "License");
dnl    you may not use this file except in compliance with the License.
dnl    You may obtain a copy of the License at
dnl 
dnl        http://www.apache.org/licenses/LICENSE-2.0
dnl 
dnl    Unless required by applicable law or agreed to in writing, software
dnl    distributed under the License is distributed on an "AS IS" BASIS,
dnl    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
dnl    See the License for the specific language governing permissions and
dnl    limitations under the License.
dnl

dnl 
dnl Autoconf support for configuring an external package to use oasys
dnl itself.
dnl

AC_DEFUN(AC_OASYS_CONFIG_HELP, [
cat <<EOF

Configure error finding oasys.

This script first looks for an oasys installation in the same location
as the tool that is using the oasys library (where this configure
script is run from). In other words, it first tries ../oasys.

If ../oasys does not exist, it looks for an oasys installation in
/usr. If that is not found, you need to specify the location of oasys
using the --with-oasys argument to configure.

If problems still exist, then look in the config.log to see exactly
what the failing command was.

EOF

])

dnl
dnl AC_OASYS_CONFIG [major] [minor]
dnl
AC_DEFUN(AC_OASYS_CONFIG, [
    rm -f oasys

    ac_oasysver_major=$1
    ac_oasysver_minor=$2
    ac_oasysdir=

    AC_ARG_WITH(oasys,
        AC_HELP_STRING([--with-oasys=DIR],
    		   [location of an oasys installation (default ../oasys or /usr)]),
        ac_oasysdir=$withval) 

    AC_MSG_CHECKING([for an oasys installation (version $ac_oasysver_major.$ac_oasysver_minor or better)])

    if test "$ac_oasysdir" = "" ; then
       if test -d ../oasys ; then
          ac_oasysdir=../oasys
       else
          ac_oasysdir=/usr
       fi
    fi

    #
    # Set the oasys paths properly. OASYS_INCDIR points to the parent
    # directory containing the oasys header files or the source
    # directory itself with a symlink (include/oasys -> .),
    # OASYS_LIBDIR points to the directory where the libraries are.
    # OASYS_ETCDIR points to where the various scripts are.
    #
    OASYS_INCDIR="$ac_oasysdir/include"
    OASYS_LIBDIR="$ac_oasysdir/lib"
    OASYS_ETCDIR="$ac_oasysdir/share/oasys"

    if test ! -d $OASYS_INCDIR ; then
       AC_MSG_ERROR(nonexistent oasys include directory $OASYS_INCDIR)
    fi

    if test ! -d $OASYS_LIBDIR ; then
       AC_MSG_ERROR(nonexistent oasys library directory $OASYS_LIBDIR)
    fi

    if test ! -d $OASYS_ETCDIR ; then
       AC_MSG_ERROR(nonexistent oasys tools directory $OASYS_ETCDIR)
    fi

    if test "$ac_oasysver_major" = "" ; then
       ac_oasysver_major=1
    fi

    if test "$ac_oasysver_minor" = "" ; then
       ac_oasysver_minor=1
    fi

    AC_OASYS_SUBST_CONFIG

    #
    # Check the settings to make sure that we can build a program.
    #
    ac_save_CFLAGS=$CFLAGS
    CFLAGS="$CFLAGS -I$OASYS_INCDIR"
    
    ac_save_LDFLAGS=$LDFLAGS
    LDFLAGS="$LDFLAGS $OASYS_LDFLAGS"
    AC_LINK_IFELSE(
      AC_LANG_PROGRAM(
      [
	#include <oasys/oasys-version.h>
	#if (OASYS_VERSION_MAJOR != ${ac_oasysver_major}) || \
            (OASYS_VERSION_MINOR <  ${ac_oasysver_minor})
	#error incorrect oasys version (wanted ${ac_oasysver_major}.${ac_oasysver_minor})
	#endif
      ], 
      [
          const char* s = oasys_version;
	  (void)s;
      ]),
      [ 
          AC_MSG_RESULT($ac_oasysdir)
      ],
      [
          AC_MSG_RESULT([no])
	  AC_MSG_ERROR([can't find compatible oasys install (version $ac_oasysver_major.$ac_oasysver_minor or better) at $ac_oasysdir])
      ])

    #
    # Create a symlink to the oasys directory for the test scripts
    #
    ln -s $ac_oasysdir oasys

    CFLAGS=$ac_save_CFLAGS
    LDFLAGS=$ac_save_LDFLAGS
])

AC_DEFUN(AC_OASYS_SUBST_CONFIG, [
    #
    # XXX/demmer is there a better way to make the paths absolute??
    #
    OASYS_INCDIR=`cd $OASYS_INCDIR && pwd`
    OASYS_LIBDIR=`cd $OASYS_LIBDIR && pwd`
    OASYS_ETCDIR=`cd $OASYS_ETCDIR && pwd`

    OASYS_VERSION=`$OASYS_ETCDIR/tools/extract-version $OASYS_ETCDIR/oasys-version.dat version`

    #
    # By default, oasys apps link statically to make it easier when
    # linking with a source directory.
    #

    if test -f $OASYS_LIBDIR/liboasys-$OASYS_VERSION.a ; then
	OASYS_LDFLAGS="$OASYS_LIBDIR/liboasys-$OASYS_VERSION.a"
	OASYS_COMPAT_LDFLAGS="$OASYS_LIBDIR/liboasyscompat-$OASYS_VERSION.a"

    elif test "$ac_oasysdir" = /usr ; then
	OASYS_LDFLAGS="-loasys-$OASYS_VERSION"
	OASYS_COMPAT_LDFLAGS="-loasyscompat-$OASYS_VERSION"

    else
	OASYS_LDFLAGS="-L$OASYS_LIBDIR -loasys-$OASYS_VERSION"
	OASYS_COMPAT_LDFLAGS="-L$OASYS_LIBDIR -loasyscompat-$OASYS_VERSION"
    fi
        
    AC_SUBST(OASYS_INCDIR)
    AC_SUBST(OASYS_LIBDIR)
    AC_SUBST(OASYS_ETCDIR)
    AC_SUBST(OASYS_LDFLAGS)
    AC_SUBST(OASYS_COMPAT_LDFLAGS)
    AC_SUBST(OASYS_VERSION)

    AC_SUBST(EXTLIB_CFLAGS)
    AC_SUBST(EXTLIB_LDFLAGS)

    AC_SUBST(SYS_EXTLIB_CFLAGS)
    AC_SUBST(SYS_EXTLIB_LDFLAGS)
])
