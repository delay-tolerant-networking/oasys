dnl
dnl License Agreement
dnl 
dnl NOTICE
dnl This software (or technical data) was produced for the U. S.
dnl Government under contract W15P7T-05-C-F600, and is
dnl subject to the Rights in Data-General Clause 52.227-14 (JUNE 1987)
dnl 
dnl Copyright (C) 2006. The MITRE Corporation (http://www.mitre.org/).
dnl All Rights Reserved.
dnl 
dnl Redistribution and use in source and binary forms, with or without
dnl modification, are permitted provided that the following conditions
dnl are met:
dnl 
dnl * Redistributions of source code must retain the above copyright
dnl notice, this list of conditions and the following disclaimer.
dnl 
dnl * Redistributions in binary form must reproduce the above copyright
dnl notice, this list of conditions and the following disclaimer in the
dnl documentation and/or other materials provided with the distribution.
dnl 
dnl * The US Government will not be charged any license fee and/or
dnl royalties related to this software.
dnl 
dnl * Neither name of The MITRE Corporation; nor the names of its
dnl contributors may be used to endorse or promote products derived from
dnl this software without specific prior written permission.
dnl 
dnl THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
dnl ``AS IS'' AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
dnl LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
dnl A PARTICULAR PURPOSE ARE DISCLAIMED.  IN NO EVENT SHALL THE COPYRIGHT
dnl HOLDERS OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT,
dnl INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING,
dnl BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS
dnl OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
dnl ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR
dnl TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE
dnl USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
dnl

dnl
dnl Autoconf support for finding xerces-c
dnl

AC_DEFUN(AC_OASYS_CONFIG_XERCES, [
    dnl
    dnl Handle --with-xerces-c=DIR
    dnl
    AC_ARG_WITH(xerces-c,
        AC_HELP_STRING([--with-xerces-c=DIR],
            [location of a xerces-c installation (default try)]),
        [ac_with_xerces_c=$withval],
        [ac_with_xerces_c=try])

    AC_MSG_CHECKING([whether xerces-c support should be enabled])

    dnl
    dnl Disable xerces-c if requested
    dnl
    if test "$ac_with_xerces_c" = no; then
        AC_MSG_RESULT([no])
        XERCES_C_ENABLED=0
    else

    dnl
    dnl Find the xerces-c installation
    dnl
    if test "$ac_with_xerces_c" = try \
         -o "$ac_with_xerces_c" = yes \ 
         -o "$ac_with_xerces_c" = "" ; then
        AC_MSG_RESULT([try])
        ac_xerces_inst_dirs="/usr /usr/local"
    else
        AC_MSG_RESULT([yes (dir $ac_with_xerces_c)])
        ac_xerces_inst_dirs="$ac_with_xerces_c"
    fi

    ac_save_CPPFLAGS="$CPPFLAGS"
    ac_save_LDFLAGS="$LDFLAGS"
    ac_save_LIBS="$LIBS"

    AC_MSG_CHECKING([whether xerces-c (>= v2.7.0) was found])
    AC_CACHE_VAL(oasys_cv_path_xerces_c,
    [
        for ac_xerces_inst_dir in $ac_xerces_inst_dirs; do
            if test -d "$ac_xerces_inst_dir"; then
                AC_LANG([C++])
                CPPFLAGS="-I$ac_xerces_inst_dir/include"
                LDFLAGS="-L$ac_xerces_inst_dir/lib"
                LIBS="-lxerces-c"

                AC_LINK_IFELSE(
                    AC_LANG_PROGRAM(
                        [
                            #include <xercesc/util/PlatformUtils.hpp>
                            #include <xercesc/util/XMLString.hpp>
                            #include <xercesc/dom/DOM.hpp>
                        ],
    
                        [
                            #if _XERCES_VERSION >= 20700

                            xercesc::XMLPlatformUtils::Initialize();
                            {
                                xercesc::DOMImplementation* impl
                                    = xercesc::DOMImplementationRegistry::getDOMImplementation
                                        (xercesc::XMLString::transcode("XML 1.0"));
                            }
                            xercesc::XMLPlatformUtils::Terminate();

                            #else
                                #error
                            #endif
                        ]),
                    [
                        oasys_cv_path_xerces_c="$ac_xerces_inst_dir"
                        break
                    ],
                    [
                        oasys_cv_path_xerces_c=
                    ]
                )
            fi
        done
    ])

    CPPFLAGS="$ac_save_CPPFLAGS"
    LDFLAGS="$ac_save_LDFLAGS"
    LIBS="$ac_save_LIBS"

    if test -z "$oasys_cv_path_xerces_c"; then
        AC_MSG_RESULT([no])
        XERCES_C_ENABLED=0
    else
	AC_MSG_RESULT([yes])
        XERCES_C_ENABLED=1
        AC_DEFINE(XERCES_C_ENABLED, 1, [whether xerces support is enabled])
        if test ! "$oasys_cv_path_xerces_c" = /usr ; then
            OASYS_CPPFLAGS="$OASYS_CPPFLAGS -I$oasys_cv_path_xerces_c/include"
            OASYS_LDFLAGS="$OASYS_LDFLAGS -L$oasys_cv_path_xerces_c/lib"
        fi
        OASYS_LIBS="$OASYS_LIBS -lxerces-c"
    fi

    fi
])
