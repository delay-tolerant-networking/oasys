dnl
dnl Autoconf support for finding Berkeley DB
dnl

AC_DEFUN(AC_OASYS_DB_HELP, [
cat <<EOF

Configure error with Berkeley DB...

If your installed version is not one of [$dbversions], you may
have to specify it with --with-dbver.

If your installation is in a non-standard path, you can specify
it with --with-db=DIR.

To download the latest version, go to http://www.sleepycat.com
To build and install to /usr/local/BerkeleyDB-<version>:

# cd <db_download_dir>/build_unix
# ../dist/configure --enable-cxx
# make
# make install

EOF

])

dnl
dnl Main macro for finding a usable db installation 
dnl
AC_DEFUN(AC_OASYS_CONFIG_DB, [
    ac_dbvers='4.3 4.2'
    ac_dbdir='yes'

    AC_ARG_WITH(db,
        AC_HELP_STRING([--with-db=DIR],
    		   [location of a Berkeley DB installation (default system)]),
        ac_dbdir=$withval) 

    AC_ARG_WITH(dbver,
        AC_HELP_STRING([--with-dbver=VERSION],
    		   Berkeley DB versions to try (default 4.3 or 4.2)),
        ac_dbvers=$withval)

    dnl
    dnl First make sure we even want it
    dnl
    if test "$ac_dbdir" = no ; then
    LIBDB_ENABLED=0
    else

    LIBDB_ENABLED=1
    AC_DEFINE_UNQUOTED(LIBDB_ENABLED, 1, 
        [whether berkeley db storage support is enabled])

    dnl
    dnl Now check if we have a cached value, unless the user specified
    dnl something explicit with the --with-db= argument, in
    dnl which case we force it to redo the checks (i.e. ignore the
    dnl cached values)
    dnl
    if test "$ac_dbdir" = yes -a ! x$oasys_cv_path_db_h = x ; then
        echo "checking for Berkeley DB installation... (cached) $oasys_cv_path_db_h/db.h, $oasys_cv_path_db_lib -l$oasys_cv_lib_db"
    else
        AC_OASYS_FIND_DB
    fi

    if test ! $oasys_cv_path_db_h = /usr/include ; then
        OASYS_CPPFLAGS="$OASYS_CPPFLAGS -I$oasys_cv_path_db_h"
    fi

    if test ! $oasys_cv_path_db_lib = /usr/lib ; then
        OASYS_LDFLAGS="$OASYS_LDFLAGS -L$oasys_cv_path_db_lib"
    fi

    if test x"$STATIC" = x"yes" ; then
        OASYS_LIBS="$OASYS_LIBS -Wl,-Bstatic -l$oasys_cv_lib_db -Wl,-Bdynamic"
    else
    	OASYS_LIBS="$OASYS_LIBS -l$oasys_cv_lib_db"
    fi

    fi # LIBDB_ENABLED
])

dnl
dnl Find db
dnl
AC_DEFUN(AC_OASYS_FIND_DB, [
    oasys_cv_path_db_h=
    oasys_cv_path_db_lib=
    oasys_cv_lib_db=

    ac_save_CPPFLAGS="$CPPFLAGS"
    ac_save_LDFLAGS="$LDFLAGS"
    ac_save_LIBS="$LIBS"

    for ac_dbver in $ac_dbvers ; do

    ac_dbver_major=`echo $ac_dbver | cut -d . -f1`
    ac_dbver_minor=`echo $ac_dbver | cut -d . -f2`

    if test "$ac_dbdir" = system -o \
            "$ac_dbdir" = yes -o \
            "$ac_dbdir" = "" ; 
    then
        ac_dbincdirs="/usr/include /usr/local/include/db$ac_dbver"
        ac_dbincdirs="$ac_dbincdirs /usr/include/db$ac_dbver"
        ac_dbincdirs="$ac_dbincdirs /usr/local/BerkeleyDB.$ac_dbver/include"
	tmp=`echo $ac_dbver | sed 's/\.//'`
	ac_dbincdirs=/usr/local/include/db$tmp

        ac_dblibdirs="/usr/lib /usr/local/lib /usr/local/include/db$ac_dbver"
        ac_dblibdirs="$ac_dblibdirs /usr/local/BerkeleyDB.$ac_dbver/lib"
	ac_dblibdirs=/usr/local/lib/db$tmp
    else
        ac_dbincdirs="$ac_dbdir/include"
        ac_dblibdirs="$ac_dbdir/lib"
    fi

    for ac_dbincdir in $ac_dbincdirs; do
	CPPFLAGS="$ac_save_CPPFLAGS -I$ac_dbincdir"
	LDFLAGS="$ac_save_LDFLAGS"
	LIBS="$ac_save_LIBS"

	dnl
	dnl First check the version in the header file. If there's a match, 
	dnl fall through to the other check to make sure it links.
	dnl If not, then we can break out of the two inner loops.
	dnl
        AC_MSG_CHECKING([for Berkeley DB header (version $ac_dbver) in $ac_dbincdir])
	AC_LINK_IFELSE(
	  AC_LANG_PROGRAM(
	    [
                #include <db.h>
           
                #if (DB_VERSION_MAJOR != ${ac_dbver_major}) || \
                    (DB_VERSION_MINOR != ${ac_dbver_minor})
                #error "incorrect version"
                #endif
            ],
            
            [
            ]),
          [ 
	      AC_MSG_RESULT([yes])
          ],
          [
              AC_MSG_RESULT([no])
	      continue
          ])

      for ac_dblibdir in $ac_dblibdirs; do
      for ac_dblib    in db-$ac_dbver; do

	LDFLAGS="$ac_save_LDFLAGS -L$ac_dblibdir"
	if test x"$STATIC" = x"yes" ; then
		LIBS="$ac_save_LIBS -Wl,-Bstatic -l$ac_dblib -Wl,-Bdynamic"
	else
		LIBS="$ac_save_LIBS -l$ac_dblib"
	fi

        AC_MSG_CHECKING([for Berkeley DB library in $ac_dblibdir, -l$ac_dblib])
	AC_LINK_IFELSE(
	  AC_LANG_PROGRAM(
	    [
                #include <db.h>
            ],
            
            [
	        DB *db;
                db_create(&db, NULL, 0);
            ]),

          [
              AC_MSG_RESULT([yes])
              oasys_cv_path_db_h=$ac_dbincdir
              oasys_cv_path_db_lib=$ac_dblibdir
              oasys_cv_lib_db=$ac_dblib
              break 4
          ],
          [
              AC_MSG_RESULT([no])
          ])
    done
    done
    done
    done

    AC_DEFINE_UNQUOTED(BERKELEY_DB_VERSION, $ac_dbver, 
        [configured version of berkeley db])

    CPPFLAGS="$ac_save_CPPFLAGS"
    LDFLAGS="$ac_save_LDFLAGS"
    LIBS="$ac_save_LIBS"

    if test x$oasys_cv_path_db_h = x ; then
        AC_OASYS_DB_HELP
        AC_MSG_ERROR([can't find usable Berkeley DB installation])
    fi
])
