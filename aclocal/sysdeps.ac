dnl -------------------------------------------------------------------------
dnl Checks for system programs
dnl -------------------------------------------------------------------------
AC_DEFUN(AC_OASYS_SYSTEM_PROGRAMS, [
    AC_PROG_AWK
    AC_PROG_INSTALL
    AC_PROG_MAKE_SET
    AC_PROG_RANLIB
])

dnl -------------------------------------------------------------------------
dnl Checks for system libraries
dnl -------------------------------------------------------------------------
AC_DEFUN(AC_OASYS_SYSTEM_LIBRARIES, [
    AC_SEARCH_LIBS(pthread_create, pthread, [], 
      AC_MSG_ERROR([can't find required library function (pthread_create)]))
    AC_CHECK_FUNC(pthread_yield, [], [AC_SEARCH_LIBS(sched_yield, rt, [],
      AC_MSG_ERROR([can't find required library function (pthread_yield or sched_yield)]))])
    AC_SEARCH_LIBS(socket, socket, [],
      AC_MSG_ERROR([can't find required library function (socket)]))
    AC_SEARCH_LIBS(gethostbyname, nsl, [],
      AC_MSG_ERROR([can't find required library function (gethostbyname)]))
    AC_SEARCH_LIBS(xdr_int, rpc, [],
      AC_MSG_ERROR([can't find required library function (xdr_int)]))
])

dnl -------------------------------------------------------------------------
dnl Checks for header files.
dnl -------------------------------------------------------------------------
AC_DEFUN(AC_OASYS_SYSTEM_HEADERS, [
    AC_HEADER_STDC
    AC_CHECK_HEADERS([err.h stdint.h string.h synch.h sys/cdefs.h sys/types.h])
])

dnl -------------------------------------------------------------------------
dnl Checks for typedefs, structures, and compiler characteristics.
dnl -------------------------------------------------------------------------
AC_DEFUN(AC_OASYS_SYSTEM_TYPES, [
    AC_C_CONST
    AC_C_INLINE
    AC_C_VOLATILE
    AC_TYPE_MODE_T
    AC_TYPE_OFF_T
    AC_TYPE_SIGNAL
    AC_TYPE_SIZE_T
    AC_CHECK_TYPES([ptrdiff_t])
    AC_CHECK_TYPES([uint32_t])
    AC_CHECK_TYPES([u_int32_t])
])

dnl -------------------------------------------------------------------------
dnl Checks for library functions.
dnl -------------------------------------------------------------------------
AC_DEFUN(AC_OASYS_SYSTEM_FUNCTIONS, [
    AC_OASYS_FUNC_FPCLASSIFY
    AC_CHECK_FUNCS([getaddrinfo gethostbyname gethostbyname_r getopt_long inet_aton inet_pton pthread_yield sched_yield])
])                                
